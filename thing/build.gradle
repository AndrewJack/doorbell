apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId "technology.mainthread.apps.watchkeeper"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        multiDexEnabled true

        buildConfigField 'String', 'CLOUD_VISION_API_KEY', appProps["cloud_vision_api_key"] != null ? "\"${appProps["cloud_vision_api_key"]}\"" : '""'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        if (keyPropsDebug["store"] != null) {
            debug {
                storeFile keyPropsDebug["store"] != null ? file(keyPropsDebug["store"]) : null
                keyAlias keyPropsDebug["alias"] ?: ""
                storePassword keyPropsDebug["storePass"] ?: ""
                keyPassword keyPropsDebug["pass"] ?: ""
            }
        }

        release {
            storeFile keyPropsRelease["store"] != null ? file(keyPropsRelease["store"]) : null
            keyAlias keyPropsRelease["alias"] ?: ""
            storePassword keyPropsRelease["storePass"] ?: ""
            keyPassword keyPropsRelease["pass"] ?: ""
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix '_debug_' + gitSha()
            applicationIdSuffix '.debug'
            versionNameSuffix " debug"
            resValue "string", "app_name", "Watchkeeper debug"
        }
        release {
            if (keyPropsRelease["store"] != null) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Watchkeeper"
        }
    }
}

dependencies {
    provided 'com.google.android.things:androidthings:0.1-devpreview'
    compile "com.google.firebase:firebase-core:${commonPlayServicesVersion}"
    compile "com.google.firebase:firebase-database:${commonPlayServicesVersion}"
    compile "com.google.firebase:firebase-messaging:${commonPlayServicesVersion}"
    compile "com.google.firebase:firebase-storage:${commonPlayServicesVersion}"
    compile 'com.google.android.things.contrib:driver-button:0.1'

    compile 'com.google.apis:google-api-services-vision:v1-rev22-1.22.0'
    compile 'com.google.api-client:google-api-client-android:1.22.0' exclude module: 'httpclient'
    compile 'com.google.http-client:google-http-client-gson:1.22.0' exclude module: 'httpclient'

    compile "io.reactivex:rxjava:${rxJavaVersion}"
    compile "io.reactivex:rxandroid:${rxAndroidVersion}"
}

apply plugin: 'com.google.gms.google-services'
